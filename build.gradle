// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.buildConfig = [
            'compileSdk' : 30,
            'minSdk'     : 21,
            'targetSdk'  : 30,
    ]

    apply from: rootProject.file('versions.gradle')

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.11.1'
}

subprojects {
    spotless {
        format 'misc', {
            // define the files to apply `misc` to
            target '**/*.gradle', '**/*.md', '**/.gitignore'

            // define the steps to apply to those files
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }

        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(Versions.ktlint)
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
    }
}

task createSpotlessPreCommitHook() {
    def gitHooksDirectory = new File("$project.rootDir/.git/hooks/")
    if (!gitHooksDirectory.exists()) gitHooksDirectory.mkdirs()
    new File("$project.rootDir/.git/hooks", "pre-commit").text ="""#!/bin/bash
echo "Running spotless check"
./gradlew spotlessApply
git add .
"""
"chmod +x .git/hooks/pre-commit".execute()
}
