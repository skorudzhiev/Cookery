// Top-level build file where you can add configuration options common to all sub-projects/modules.

import com.skorudzhiev.cookery.buildsrc.Libs
import com.skorudzhiev.cookery.buildsrc.Versions

buildscript {
    ext.buildConfig = [
            'compileSdk' : 30,
            'minSdk'     : 21,
            'targetSdk'  : 30,
    ]

    repositories {
        google()
        mavenCentral()
        maven {
            url  "https://oss.sonatype.org/content/repositories/snapshots"
            content {
                includeModule("com.google.dagger", "hilt-android-gradle-plugin")
            }
        }
    }

    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.hiltGradlePlugin
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.10'
        classpath "com.google.dagger:hilt-android-gradle-plugin:HEAD-SNAPSHOT"
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.12.5'
}

subprojects {

    apply plugin: 'com.diffplug.spotless'
    spotless {
        format 'misc', {
            // define the files to apply `misc` to
            target '**/*.gradle', '**/*.md', '**/.gitignore'

            // define the steps to apply to those files
            indentWithSpaces()
            trimTrailingWhitespace()
            endWithNewline()
        }

        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(Versions.ktlint)
            trimTrailingWhitespace()
            indentWithSpaces()
            endWithNewline()
        }
    }
}

task createSpotlessPreCommitHook() {
    def gitHooksDirectory = new File("$project.rootDir/.git/hooks/")
    if (!gitHooksDirectory.exists()) gitHooksDirectory.mkdirs()
    new File("$project.rootDir/.git/hooks", "pre-commit").text ="""#!/bin/bash
echo "Running spotless check"
./gradlew spotlessApply
git add .
"""
"chmod +x .git/hooks/pre-commit".execute()
}
